import io.micronaut.guides.GuidesPlugin
import io.micronaut.guides.IndexGenerator
import io.micronaut.guides.TestScriptGenerator
import io.micronaut.guides.ThemeProcessor
import io.micronaut.guides.GitDiffUtils

plugins {
    id 'groovy'
    id("org.asciidoctor.jvm.convert") version "4.0.0-alpha.1"
}

apply from: "gradle/asciidoc.gradle"
version '0.0.1'

repositories {
    mavenCentral()
    jcenter()
}

ext {
    codeDir = new File(buildDir, 'code')
    distDir = new File(buildDir, 'dist')
    guidesDir = new File(projectDir, 'guides')
    metadataConfigName = 'metadata.json'
    micronautVersion = new File(projectDir, 'version.txt').text.trim()
}

task cleanAsciidoctorFolder(type: Delete) {
    group 'guides'
    description 'Cleans src/docs/asciidoc directory'
    delete fileTree('src/docs/asciidoc') {
        include '*.adoc'
    }
}

apply plugin: GuidesPlugin

task copyImagesToDist(type: Copy) {
    group 'guides'
    description 'Copies images from src/docs/images to build/dist/images directory'
    from 'src/docs/images'
    into "$distDir/images"
    include '**/*.png',
            '**/*.jpg',
            '**/*.svg',
            '**/*.gif'
}

task copyStylesheetsToDist(type: Copy) {
    group 'guides'
    description 'Copies CSS files from assets/stylesheets to build/dist directory'
    from 'assets/stylesheets'
    into distDir
    include '*.css'
}

task copyJavaScriptToDist(type: Copy) {
    group 'guides'
    description 'Copies JS files from assets/js to build/dist directory'
    from 'assets/js'
    into distDir
    include '*.js'
}

task copyHtmlToDist(type: Copy) {
    group 'guides'
    description 'Copies HTML generated from Asciidoc from build/docs/asciidoc to build/dist'
    from "$buildDir/docs/asciidoc"
    into distDir
    exclude 'common-*.html'
}

task createDist {
    group 'guides'
    description 'Creates build/dist directory with static assets'
    dependsOn 'copyImagesToDist',
              'copyStylesheetsToDist',
              'copyJavaScriptToDist'
}

clean.dependsOn 'cleanAsciidoctorFolder'
build.dependsOn 'generateCodeZip'
build.dependsOn 'asciidoctor'

asciidoctor {
    inputs.dir(project.layout.projectDirectory.dir("src/docs/asciidoc"))
    // Pick up changes to included code
    inputs.dir(project.layout.projectDirectory.dir("guides"))
    mustRunAfter 'generateCodeZip'
    finalizedBy 'copyHtmlToDist'
}

task generateTestScript {
    group 'guides'
    description 'Generates a test.sh to run every guide test at build/code'
    doLast {
        codeDir.mkdirs()
        File testScript = new File(codeDir, "test.sh")
        testScript.createNewFile()
        List<String> changedFiles = []
        boolean generateTestScript = true
        if (System.getenv('GITHUB_WORKFLOW')) {
            changedFiles = GitDiffUtils.filesChanged()
            generateTestScript = !GitDiffUtils.onlyImagesOrMarkdownOrAsciidocChanged(changedFiles)
        }
        testScript.text = generateTestScript
                ? TestScriptGenerator.generateScript(guidesDir, metadataConfigName, false, changedFiles)
                : TestScriptGenerator.EMPTY_SCRIPT
        testScript.executable = true
    }
}

task generateGuidesIndex {
    group 'guides'
    description 'Generates an index.html with all the guides'
    doLast {
        if (!distDir.exists()) {
            distDir.mkdirs()
        }
        File template = new File(projectDir, "assets/template.html")
        IndexGenerator.generateGuidesIndex(template, guidesDir, distDir, metadataConfigName)
    }
}

task generateGuidesJsonMetadata {
    group 'guides'
    description 'Generates a guides.json with the metadata for all the guides'
    doLast {
        if (!distDir.exists()) {
            distDir.mkdirs()
        }
        File guidesJson = new File(distDir, 'guides.json')
        guidesJson.createNewFile()
        guidesJson.text = IndexGenerator.generateGuidesJsonIndex(guidesDir, metadataConfigName)
    }
}

task themeGuides {
    group 'guides'
    description 'Generates an index.html with all the guides'
    doLast {
        File template = new File(projectDir, "assets/template.html")
        ThemeProcessor.applyThemes(template, distDir, guidesDir, metadataConfigName)
    }
    mustRunAfter('copyHtmlToDist')
}
