plugins {
    id "java-gradle-plugin"
    id 'groovy'
    id 'nu.studer.rocker' version '3.0.4'
}

ext {
    micronautVersion = new File(projectDir.parentFile, 'version.txt').text.trim()
    micronautDocsVersion = '1.0.25'
}

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    annotationProcessor(platform("io.micronaut.platform:micronaut-platform:$micronautVersion"))
    annotationProcessor("io.micronaut:micronaut-inject-java")
    implementation(platform("io.micronaut.platform:micronaut-platform:$micronautVersion"))
    implementation("io.micronaut.rss:micronaut-jsonfeed-core")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut.starter:micronaut-starter-api:$micronautVersion")
    implementation("io.micronaut:micronaut-inject-java")
    implementation('org.apache.commons:commons-compress:1.26.0')
    implementation "io.micronaut.docs:micronaut-docs-asciidoc-extensions:$micronautDocsVersion", {
        exclude group: "org.codehaus.groovy"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/generated/rocker'
        }
    }
}

rocker {
    configurations {
        main {
            templateDir = file('src/main/java')
            outputDir = file('src/generated/rocker')
            optimize = true
        }
    }
}
